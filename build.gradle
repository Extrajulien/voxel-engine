plugins {
    id 'application'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

application {
    mainClass = 'demo_game.App'
}

repositories {
    mavenCentral()
}
def lwjglVersion = "3.3.4"
def os = System.getProperty("os.name").toLowerCase()
def arch = System.getProperty("os.arch").toLowerCase()
def lwjglNatives
if (os.contains("win")) {
    lwjglNatives = "natives-windows"
} else if (os.contains("mac")) {
    // Distinguish between Intel and Apple Silicon
    if (arch.contains("aarch64") || arch.contains("arm")) {
        lwjglNatives = "natives-macos-arm64"
    } else {
        lwjglNatives = "natives-macos"
    }
} else {
    lwjglNatives = "natives-linux"
}

run {
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        jvmArgs "-XstartOnFirstThread"
    }
}

dependencies {

    implementation "org.lwjgl:lwjgl:${lwjglVersion}"
    implementation "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    implementation "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    implementation "org.lwjgl:lwjgl-stb:${lwjglVersion}"

    implementation "org.joml:joml:1.10.8"

    runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
    runtimeOnly "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}